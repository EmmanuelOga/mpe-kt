buildscript {
    ext.kotlin_version = '1.2.21'
    ext.dokka_version = '0.9.14'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}


apply plugin: 'kotlin-platform-common'

apply plugin: 'org.jetbrains.dokka'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task jacocoFix(type: Copy) {
    from 'jacocoRaw.xml'
    into './'
    rename { String fileName ->
        fileName.replace("jacocoRaw.xml", "jacoco.xml")
    }
    filter { line -> line.replaceAll("com/", "mpe-jvm/src/main/kotlin/com/") }
}